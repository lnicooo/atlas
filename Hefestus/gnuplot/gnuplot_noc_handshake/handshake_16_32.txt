unset title
unset label
set autoscale
set title "Average Power Consumption in routers with flit size=16 bits and buffer depth=32"
set xlabel "% Total Rate"
set ylabel "Average Power Consumption (mW)"
#-------------------------------------------------------------------------------
# Buffer Equations
#-------------------------------------------------------------------------------
a_buffer=0.00619103
b_buffer=13.2255
buffer(x)=(a_buffer*x)+b_buffer
#-------------------------------------------------------------------------------
# Swicth Control Equations
#-------------------------------------------------------------------------------
a_swctrl=0.000506013
b_swctrl=1.23841
swctrl(x)=(a_swctrl*x)+b_swctrl
routerBL(x)=3*buffer(x)+ swctrl(x)
routerBC(x)=4*buffer(x)+ swctrl(x)
routerCC(x)=5*buffer(x)+ swctrl(x)
set xrange [0:50]
plot routerBL(x) title 'RouterBL, RouterBR, RouterTL, RouterTR', \
     routerBC(x) title 'RouterBC, RouterCL, RouterCR, RouterTC', \
     routerCC(x) title 'RouterCC'
pause -1 "Press ENTER to continue"