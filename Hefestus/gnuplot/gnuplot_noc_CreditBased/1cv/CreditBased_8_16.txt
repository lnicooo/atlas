unset title
unset label
set autoscale
set title "Average Power Consumption in routers with flit size=8 bits and buffer depth=16"
set xlabel "% Total Rate"
set ylabel "Average Power Consumption (mW)"
#-------------------------------------------------------------------------------
# Buffer Equations
#-------------------------------------------------------------------------------
a_buffer=0.00723786
b_buffer=3.69751
buffer(x)=(a_buffer*x)+b_buffer
#-------------------------------------------------------------------------------
# Swicth Control Equations
#-------------------------------------------------------------------------------
a_swctrl=0.00101001
b_swctrl=1.23966
swctrl(x)=(a_swctrl*x)+b_swctrl
#-------------------------------------------------------------------------------
# Crossbar Equations
#-------------------------------------------------------------------------------
a_xbar=0.00028319
b_xbar=0.000935752
xbar(x)=(a_xbar*x)+b_xbar
routerBL(x)=3*buffer(x)+ swctrl(x) + xbar(x)
routerBC(x)=4*buffer(x)+ swctrl(x) + xbar(x)
routerCC(x)=5*buffer(x)+ swctrl(x) + xbar(x)
set xrange [0:50]
plot routerBL(x) title 'RouterBL, RouterBR, RouterTL, RouterTR', \
     routerBC(x) title 'RouterBC, RouterCL, RouterCR, RouterTC', \
     routerCC(x) title 'RouterCC'
pause -1 "Press ENTER to continue"
