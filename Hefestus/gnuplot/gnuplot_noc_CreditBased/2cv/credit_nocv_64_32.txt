unset title
unset label
set autoscale
set title "Average Power Consumption in routers with flit size=64 bits and buffer depth=32"
set xlabel "% Total Rate"
set ylabel "Average Power Consumption (mW)"
#-------------------------------------------------------------------------------
# Inport Equations
#-------------------------------------------------------------------------------
a_inport=0.00219959
b_inport=107.723
inport(x)=(a_inport*x)+b_inport
#-------------------------------------------------------------------------------
# Swicth Control Equations
#-------------------------------------------------------------------------------
a_swctrl=0.000171888
b_swctrl=2.87816
swctrl(x)=(a_swctrl*x)+b_swctrl
#-------------------------------------------------------------------------------
# Outport Equations
#-------------------------------------------------------------------------------
a_ouport=0.000471135
b_ouport=0.0592387
ouport(x)=(a_ouport*x)+b_ouport
#-------------------------------------------------------------------------------
# Router Equations
#-------------------------------------------------------------------------------
routerBL(x)=3*inport(x)+ swctrl(x) + 3*ouport(x)
routerBC(x)=4*inport(x)+ swctrl(x) + 4*ouport(x)
routerCC(x)=5*inport(x)+ swctrl(x) + 5*ouport(x)
set xrange [0:50]
plot routerBL(x) title 'RouterBL, RouterBR, RouterTL, RouterTR', \
     routerBC(x) title 'RouterBC, RouterCL, RouterCR, RouterTC', \
     routerCC(x) title 'RouterCC'
pause -1 "Press ENTER to continue"	 
